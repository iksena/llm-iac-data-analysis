variable "resource_name_options" {
  description = "(Optional) Options to adjust how resource names are generated"
  type = object({
    template      = optional(string)
    template_safe = optional(string)
  })
  default = {
    template      = "$${resource_type}-$${app_name}"
    template_safe = "$${resource_type}$${app_name}"
  }

  validation {
    condition     = can(regex("^[A-Za-z0-9-]+$", templatestring(var.resource_name_options.template, { resource_type = "", app_name = "" })))
    error_message = "The template value must contain only alphanumeric characters and hyphens."
  }

  validation {
    condition     = can(regex("^[A-Za-z0-9]+$", templatestring(var.resource_name_options.template_safe, { resource_type = "", app_name = "" })))
    error_message = "The template_safe value must contain only alphanumeric characters."
  }
}

locals {
  name_template_vars = {
    app_name = var.app_name
  }
}

variable "resource_group_name" {
  description = "(Optional) Existing Resource Group name. If this is not provided, a resource group will be created automatically."
  type        = string
  default     = null
}

variable "location" {
  description = "Azure location for resources. If a resource_group_id is provided, this value is ignored."
  type        = string
  default     = "eastus2"
}

variable "app_name" {
  description = "Base name for the App Service (combined with prefix)."
  type        = string
}

locals {
  safe_app_name = replace(trim(lower(var.app_name), "-"), "/[^a-z0-9]/", "")
}

variable "app_settings" {
  description = "Additional application settings to add to the app."
  type        = map(string)
  default     = {}
}

variable "app_secrets" {
  description = "List of secrets to create and optionally bind to app settings."
  type = list(object({
    name             = string
    app_setting_name = optional(string)
    initial_value    = optional(string)
    tags             = optional(map(string))
    external         = optional(bool, false)
  }))
  default   = []
  sensitive = true

  validation {
    condition     = length([for s in var.app_secrets : s.name]) == length(distinct([for s in var.app_secrets : s.name]))
    error_message = "Each app_secrets entry must have a unique 'name'."
  }
}

variable "connection_strings" {
  description = "List of connection strings to add to the application."
  type = list(object({
    name        = string
    type        = string
    secret_name = string
  }))
  default   = []
  sensitive = false

  validation {
    condition     = length([for s in var.connection_strings : s.name]) == length(distinct([for s in var.connection_strings : s.name]))
    error_message = "Each connection_strings entry must have a unique 'name'."
  }

  validation {
    condition     = alltrue([for cs in var.connection_strings : contains([for s in var.app_secrets : s.name], cs.secret_name)])
    error_message = "Each connection_strings entry must reference a secret in app_secrets."
  }
}

variable "application_stack" {
  type = object({
    docker_image_name        = optional(string)
    docker_registry_url      = optional(string)
    docker_registry_username = optional(string)
    docker_registry_password = optional(string)
    dotnet_version           = optional(string)
    go_version               = optional(string)
    java_server              = optional(string)
    java_server_version      = optional(string)
    java_version             = optional(string)
    node_version             = optional(string)
    php_version              = optional(string)
    python_version           = optional(string)
    ruby_version             = optional(string)
  })
  default = {}
}

variable "site_config" {
  type = object({
    always_on                          = optional(bool)
    api_definition_url                 = optional(string)
    api_management_api_id              = optional(string)
    app_command_line                   = optional(string)
    client_affinity_enabled            = optional(bool)
    client_certificate_enabled         = optional(bool)
    client_certificate_exclusion_paths = optional(string)
    client_certificate_mode            = optional(string)
    default_documents                  = optional(list(string))
    ftps_state                         = optional(string)
    health_check_path                  = optional(string)
    health_check_eviction_time_in_min  = optional(number)
    http2_enabled                      = optional(bool, true)
    https_only                         = optional(bool)
    load_balancing_mode                = optional(string)
    minimum_tls_version                = optional(string)
    use_32_bit_worker                  = optional(bool, false)
    virtual_network_subnet_id          = optional(string)
    vnet_route_all_enabled             = optional(bool)
    websockets_enabled                 = optional(bool)
    worker_count                       = optional(number)
  })
  default = {}
}

variable "cors" {
  type = object({
    allowed_origins     = optional(list(string))
    support_credentials = optional(bool)
  })
  default = {}
}

variable "service_plan_sku" {
  description = "App Service Plan size within the tier, e.g., B2."
  type        = string
  default     = "B2"
}

variable "service_plan_id" {
  description = "Existing App Service Plan ID. If this is not provided, a new plan will be created."
  type        = string
  default     = null
}

variable "log_analytics_workspace_id" {
  description = "Optional Log Analytics Workspace ID. If provided, App Insights is workspace-based and diagnostics will send logs/metrics to LAW."
  type        = string
  default     = null
}

variable "diagnostic_log_category_groups" {
  description = "List of log category groups to enable for diagnostic settings."
  type        = list(string)
  default     = ["allLogs"]
}

variable "diagnostic_log_categories" {
  description = "List of log categories to enable for diagnostic settings."
  type        = list(string)
  default     = []
}

variable "diagnostic_metric_categories" {
  description = "List of metric categories to enable for diagnostic settings."
  type        = list(string)
  default     = ["AllMetrics"]
}

variable "global_tags" {
  description = "Tags to apply to all resources (e.g., environment, cost-center)."
  type        = map(string)
  default     = {}
}

variable "enable_system_assigned_identity" {
  description = "Enable system-assigned managed identity on the app (in addition to the user-assigned one)."
  type        = bool
  default     = false
}

variable "key_vault" {
  type = object({
    sku                        = optional(string, "standard")
    purge_protection_enabled   = optional(bool, false)
    soft_delete_retention_days = optional(number, 7)
    existing_name              = optional(string, null)
    existing_rg_name           = optional(string, null)
  })
  default = {}
}

variable "private_acr_id" {
  description = "Optional ID of a private ACR for pulling container images"
  type        = string
  default     = null
}